# Contributor: Patrick Jackson <PatrickSJackson gmail com>
# Maintainer: Christoph Vigano <mail@cvigano.de>

pkgname=ava_st
pkgver=0.8.1
pkgrel=1
pkgdesc='A simple virtual terminal emulator for X. Applyed scroll, alpha, icon hide...'
arch=('i686' 'x86_64')
license=('MIT')
depends=('libxft' 'libxext' 'xorg-fonts-misc')
provides=('st')
makedepends=('ncurses')
url="http://st.suckless.org"
source=(https://github.com/alexvanaxe/ava_st/archive/home.zip)
sha256sums=('fb32014ba506648db4c339dde9615752313547ac7e13eede22d9556d8eda5ee6')

prepare() {
  cd $srcdir/ava_st-home
  # skip terminfo which conflicts with nsurses
  sed -i '/tic /d' Makefile
}

build() {
  cd $srcdir/ava_st-home
  make X11INC=/usr/include/X11 X11LIB=/usr/lib/X11
}

package() {
  cd $srcdir/ava_st-home
  make PREFIX=/usr DESTDIR="$pkgdir" TERMINFO="$pkgdir/usr/share/terminfo" install
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
  install -Dm644 README "$pkgdir/usr/share/doc/$pkgname/README"
}
